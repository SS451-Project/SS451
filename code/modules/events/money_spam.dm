/datum/event/pda_spam
	endWhen = 36000
	var/last_spam_time = 0
	var/obj/machinery/message_server/useMS

/datum/event/pda_spam/setup()
	last_spam_time = world.time
	pick_message_server()

/datum/event/pda_spam/proc/pick_message_server()
	if(GLOB.message_servers)
		for(var/obj/machinery/message_server/MS in GLOB.message_servers)
			if(MS.active)
				useMS = MS
				break

/datum/event/pda_spam/tick()
	if(world.time > last_spam_time + 3000)
		//if there's no spam managed to get to receiver for five minutes, give up
		kill()
		return

	if(!useMS || !useMS.active)
		useMS = null
		pick_message_server()

	if(useMS)
		if(prob(5))
			// /obj/machinery/message_server/proc/send_pda_message(var/recipient = "",var/sender = "",var/message = "")
			var/list/viables = list()
			for(var/obj/item/pda/check_pda in GLOB.PDAs)
				var/datum/data/pda/app/messenger/check_m = check_pda.find_program(/datum/data/pda/app/messenger)

				if(!check_m || !check_m.can_receive())
					continue
				viables.Add(check_pda)

			if(!viables.len)
				return
			var/obj/item/pda/P = pick(viables)
			var/datum/data/pda/app/messenger/PM = P.find_program(/datum/data/pda/app/messenger)

			var/sender
			var/message
			switch(pick(1,2,3,4,5,6,7))
				if(1)
					sender = pick("MaxBet","Онлайн казино MaxBet","Нет лучшего времени для регистрации","Я очень рад, что ты присоединишься к нам")
					message = pick("Тройные депозиты ждут вас на сайте MaxBet, когда вы зарегистрируетесь, чтобы играть с нами.",\
					"Вы можете претендовать на 200% бонуса в MaxBet сегодня.",\
					"Как только вы станете игроком MaxBet, вы также будете получать выгодные еженедельные и ежемесячные акции.",\
					"В MaxBet вы сможете насладиться более чем 450 казино игр высшего класса.")
				if(2)
					sender = pick(300;"Система Быстрых Знакомств",200;"Найди свою русскую невесту",50;"Таяранские красавицы ждут",50;"Найди свою тайную любовь к скреллу",50;"Прекрасные невесты-унатхи")
					message = pick("Ваш профиль привлек мое внимание, и я хотел написать и поздороваться (Быстрое знакомство).",\
					"Если вы напишете мне на мою электронную почту [pick(GLOB.first_names_female)]@[pick(GLOB.last_names_female)].[pick("ru","ck","tj","ur","nt")] Я обязательно пришлю вам фотографию (Быстрое знакомство).",\
					"Я хочу, чтобы мы писали друг другу, и я надеюсь, что вам понравится мой профиль и вы ответите мне (Быстрое знакомство).",\
					"У вас (1) новое сообщение!",\
					"У вас (2) новых гостей!")
				if(3)
					sender = pick("Галактическая платежная ассоциация","Бюро лучшего бизнеса","Электронные платежи Nyx","Финансовый отдел NAnoTransen","Роскошные Копии")
					message = pick("Роскошные часы по бросовым ценам!",\
					"Часы, Ювелирные Изделия и Аксессуары, Сумки и Кошельки !",\
					"Внесите 100$ и получите 300$ совершенно бесплатно!",\
					" 100K NT.|WOWGOLD ?лько $89            <ГОРЯЧЕЕ>",\
					"Нам была подана жалоба от одного из ваших клиентов в отношении их деловых отношений с вами.",\
					"Мы убедительно просим вас открыть ОТЧЕТ О ЖАЛОБЕ (прилагается), чтобы ответить на эту жалобу..")
				if(4)
					sender = pick("Купить Доктор Максман","Испытываете дисфункциональные проблемы?")
					message = pick("ДОКТОР МАКСМАН: НАСТОЯЩИЕ Врачи, НАСТОЯЩАЯ Наука, РЕАЛЬНЫЕ Результаты!",\
					"Доктор Максман был создан Джорджем Агиларом, доктором медицинских наук, сертифицированным урологом CentCom, который лечил более 70 000 пациентов по всему сектору с 'мужскими проблемами'.",\
					"После семи лет исследований доктор Агилар и его команда придумали эту простую и прорывную формулу повышения мужской силы.",\
					"Мужчины всех видов сообщают об УДИВИТЕЛЬНОМ увеличении длины, ширины и выносливости.")
				if(5)
					sender = pick("Доктор","Наследный принц","Король-регент","Профессор","Капитан")
					sender += " " + pick("Роберт","Альфред","Джозеф","Кингсли","Сехи","Збаши")
					sender += " " + pick("Мугаве","Нькем","Гбатоквия","Шеквуб","Надим","Ндубиси")
					message = pick("ВАШ ФОНД БЫЛ ПЕРЕВЕДЕН В БАНК [pick("САЛУСА","СЕГУНДУ","ЦЕВУС","АНДРОМЕДУ","ГРУИС","КОРОНУ","АКВИЛУ","АРЕС","АССЕЛУС")] ДЛЯ ДАЛЬНЕЙШЕГО ПЕРЕВОДА ДЕНЕЖНЫХ СРЕДСТВ.",\
					"Мы рады сообщить вам, что в связи с задержкой нам было поручено НЕМЕДЛЕННО внести все средства на ваш счет",\
					"Уважаемый получатель средств, Мы сообщаем вам, что просроченный платеж средств наконец одобрен и выпущен для оплаты",\
					"Из-за отсутствия у меня агентов мне требуется финансовый счет за пределами мира, чтобы немедленно внести сумму в размере 1 и ПЯТИ ДЕСЯТЫХ МИЛЛИОНА кредитов.",\
					"Приветствую вас, сэр, я с сожалением сообщаю вам, что умираю. Из-за отсутствия наследников, я выбрал вас для получения полной суммы моих пожизненных сбережений в размере 1,5 миллиарда кредитов")
				if(6)
					sender = pick("Отдел морального духа Nanotrasen","Чувствуешь себя одиноким?","Скучно?","www.wetskrell.nt")
					message = pick("Отдел морального духа Nanotrasen желает предоставить вам качественные развлекательные сайты.",\
					"WetSkrell.nt это ксенофилический веб-сайт, одобренный NT для использования членами экипажа мужского пола среди множества станций и аванпостов.",\
					"Wetskrell.nt обеспечивает высочайшее качество мужского развлечения для сотрудников Nanotrasen.",\
					"Просто введите номер своего банковского счета Nanotrasen и PIN-код. С помощью трех простых шагов эта услуга может стать вашей!")
				if(7)
					sender = pick("Вы выиграли бесплатные билеты!","Нажмите здесь, чтобы получить свой приз!","Вы 1000-й посетитель!","Вы наш счастливый обладатель главного приза!")
					message = pick("Вы выиграли билеты на новейший боевик ДЖЕКСОНА!",\
					"Вы выиграли билеты на новейшую детективную драмму 'ДЕТЕКТИВНАЯ ТАЙНА В ДЕЛЕ О КАТАСТРОФЕ'!",\
					"Вы выиграли билеты на новейшую романтическую комедию '16 ПРАВИЛ ЛЮБВИ'!",\
					"Вы выиграли билеты на новейший триллер 'КУЛЬТ СПЯЩЕГО'!")

			if(useMS.send_pda_message("[P.owner]", sender, message))	//Message been filtered by spam filter.
				return

			last_spam_time = world.time

			if(prob(50)) //Give the AI an increased chance to intercept the message
				for(var/mob/living/silicon/ai/ai in GLOB.mob_list)
					// Allows other AIs to intercept the message but the AI won't intercept their own message.
					if(ai.aiPDA != P && ai.aiPDA != src)
						ai.show_message("<i>Перехваченное сообщение от <b>[sender]</b></i> (Неизвестно / СПАМ?) <i>для <b>[P:owner]</b>: [message]</i>")

			PM.notify("<b>Сообщение от [sender] (Неизвестно / СПАМ?), </b>\"[message]\" (Ответ закрыт отправителем)", 0)
